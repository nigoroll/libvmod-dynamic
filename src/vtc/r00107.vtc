varnishtest "regression test issue #107 dom_release() from unref_domains"

feature cmd "getent hosts localhost www.localhost img.localhost"

server s1 {
       rxreq
       expect req.http.Host == localhost
       txresp

       rxreq
       expect req.http.Host == www.localhost
       txresp

       rxreq
       expect req.http.Host == www.localhost
       txresp
} -start

varnish v1 -vcl+backend {
	import dynamic;

	sub vcl_init {
		new d1 = dynamic.director(
			port = "${s1_port}",
			ttl = 1m,
			domain_usage_timeout = 900ms);
	}

	sub vcl_recv {
		set req.backend_hint = d1.backend(req.http.Host);
		return (pass);
	}

	sub vcl_backend_error {
		# the director may resolve ::1 first
		return (retry);
	}
} -start

client c1 {
	txreq -hdr "Host: localhost"
	rxresp
	expect resp.status == 200
} -run

delay 1

varnish v1 -vcl {backend proforma none;}

varnish v1 -cliok "vcl.state vcl1 cold"
