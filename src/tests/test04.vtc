varnishtest "Domain timeout"

feature cmd "getent hosts localhost www.localhost img.localhost"

server s1 {
       rxreq
       expect req.http.Host == localhost
       txresp

       rxreq
       expect req.http.Host == www.localhost
       txresp

       rxreq
       expect req.http.Host == www.localhost
       txresp
} -start

varnish v1 -vcl+backend {
	import ${vmod_dynamic};

	sub vcl_init {
		new d1 = dynamic.director(
			port = "${s1_port}",
			ttl = 100ms,
			domain_usage_timeout = 200ms);
	}

	sub vcl_recv {
		set req.backend_hint = d1.backend(req.http.Host);
	}

	sub vcl_backend_error {
		# the director may resolve ::1 first
		return (retry);
	}
} -start

logexpect l1 -v v1 {
	expect * * VCL_Log "vmod-dynamic: vcl1 d1 localhost:[0-9]+ timeout"
	expect * * VCL_Log "vmod-dynamic: vcl1 d1 localhost:[0-9]+ deleted"
} -start

client c1 {
	txreq -hdr "Host: localhost"
	rxresp
	expect resp.status == 200

	delay 1

	# mark localhost as stale
	txreq -url "/foo" -hdr "Host: www.localhost"
	rxresp
	expect resp.status == 200

	delay 1

	# evict localhost
	txreq -url "/bar" -hdr "Host: www.localhost"
	rxresp
	expect resp.status == 200
} -run

varnish v1 -expect VBE.vcl1.d1(${s1_addr}).req == 3

logexpect l1 -wait
