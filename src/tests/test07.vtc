varnishtest "debugging"

feature cmd "getent hosts localhost www.localhost img.localhost"

server s1 {
	rxreq
	txresp

	accept
	rxreq
	txresp

	accept
	rxreq
	txresp
} -start

varnish v1 -vcl {
	import ${vmod_dynamic};

	backend proforma none;

	acl ipv4_loopback +log {
		"127/24";
	}

	sub vcl_init {
		new d1 = dynamic.director(
			port = "${s1_port}",
			whitelist = ipv4_loopback,
			domain_usage_timeout = 1s);
		d1.debug(true);
	}

	sub vcl_recv {
		set req.backend_hint = d1.backend();
	}
} -start

logexpect l1 -v v1 -g raw {
	expect * * VCL_acl	"^MATCH ipv4_loopback"
	expect * * Debug	"^vmod-dynamic vcl1 d1 localhost:[0-9]+ new-backend d1.127.0.0.1:[0-9]+."
	expect * * VCL_Log	"^vmod-dynamic vcl1 d1 localhost:[0-9]+ timeout"
	expect * * VCL_Log	"^vmod-dynamic vcl1 d1 localhost:[0-9]+ deleted"
	expect * * Debug	"^vmod-dynamic vcl1 d1 localhost:[0-9]+ unref-backend d1.127.0.0.1:[0-9]+"
	expect * * VCL_acl	"^MATCH ipv4_loopback "
	expect * * Debug	"^vmod-dynamic vcl1 d1 img.localhost:[0-9]+ new-backend d1.127.0.0.1:[0-9]+."
	expect * * VCL_acl	"^MATCH ipv4_loopback "
	expect * * Debug	"^vmod-dynamic vcl1 d1 www.localhost:[0-9]+ ref-backend d1.127.0.0.1:[0-9]+"
} -start

client c1 {
	txreq -hdr "Host: localhost"
	rxresp
	expect resp.status == 200

	delay 1.5

	txreq -hdr "Host: img.localhost"
	rxresp
	expect resp.status == 200

	txreq -hdr "Host: www.localhost"
	rxresp
	expect resp.status == 200
} -run

logexpect l1 -wait
