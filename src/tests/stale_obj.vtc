varnishtest "#45 regression: std.healthy on stale object"

feature cmd "getent hosts localhost"

server s1 {
	rxreq
	expect req.http.Is-Bgfetch == false
	txresp

	rxreq
	expect req.http.Is-Bgfetch == true
	txresp
} -start

varnish v1 -vcl+backend {
	import ${vmod_dynamic};
	import std;

	sub vcl_init {
		new d1 = dynamic.director(port = "${s1_port}");
	}

	sub vcl_recv {
		set req.backend_hint = d1.backend("localhost");
	}

	sub vcl_hit {
		set req.http.healthy = std.healthy(req.backend_hint);
	}

	sub vcl_deliver {
		set resp.http.healthy = req.http.healthy;
		set resp.http.ttl = obj.ttl;
	}

	sub vcl_backend_fetch {
		set bereq.http.Is-Bgfetch = bereq.is_bgfetch;
	}

	sub vcl_backend_response {
		set beresp.ttl = 0.001s;
		set beresp.grace = 1m;
	}

	sub vcl_backend_error {
		# the director may resolve ::1 first
		return (retry);
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.healthy == ""

	txreq
	rxresp
	expect resp.status == 200
	expect resp.http.healthy == true
	expect resp.http.ttl ~ {^-.*}
} -run

varnish v1 -expect VBE.vcl1.d1(${s1_addr}).req == 2
varnish v1 -expect LCK.dynamic.director.creat > 0
varnish v1 -expect LCK.dynamic.backend.creat > 0
