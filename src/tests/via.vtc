varnishtest "via"

shell "getent hosts localhost www.localhost img.localhost || true"

server s1 {
	rxreq
	txresp
} -start

varnish v2 -proto PROXY -vcl {
	import ${vmod_dynamic};
	import std;

	backend dummy { .host = "${bad_backend}"; }

	sub vcl_init {
		new d1 = dynamic.director();
	}

	sub vcl_recv {
		set req.backend_hint = d1.backend(server.ip,
		    std.port(server.ip));
		return (pass);
	}
} -start

varnish v1 -vcl {
	import ${vmod_dynamic};

	backend v2 { .host = "${v2_addr}"; .port = "${v2_port}"; }

	sub vcl_init {
		new d1 = dynamic.director(
		    port = "${s1_port}",
		    via = v2);
	}

	sub vcl_recv {
		set req.backend_hint = d1.backend("localhost");
	}

	sub vcl_backend_error {
		# the director may resolve ::1 first
		return (retry);
	}
} -start

client c1 {
	txreq
	rxresp
	expect resp.status == 200
} -run

varnish v1 -expect VBE.vcl1.d1(${s1_addr}:${s1_port}).req == 1
varnish v1 -expect LCK.dynamic.director.creat > 0
varnish v1 -expect LCK.dynamic.backend.creat > 0

# vtc diag only
shell "varnishstat -1 -n ${v1_name} -f VBE.*"
